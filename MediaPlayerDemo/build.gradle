buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/repo' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'

repositories {
    maven { url 'https://maven.fabric.io/repo' }
}

android {
    compileSdkVersion 19
    buildToolsVersion '19.1.0'

    defaultConfig {
        applicationId packageBase + '.mediaplayerdemo'
        minSdkVersion 16
        targetSdkVersion 19
        versionCode 1
        versionName '1.0'
        buildConfigField "boolean", "CRASHLYTICS_CONFIGURED", "${isChrashlyticsConfigured()}"
    }

    signingConfigs {
        debug   // configured in signingconfig.gradle
        release // configured in signingconfig.gradle
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (variant.name == android.buildTypes.release.name) {
                def file = output.outputFile
                def fileName = file.name.replace(".apk", "-" + defaultConfig.versionCode + "-" + defaultConfig.versionName + ".apk")
                output.outputFile = new File(file.parent, fileName)
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':MediaPlayer')
    compile project(':MediaPlayer-DASH')
    compile project(':MediaPlayer-GLES')
    compile project(':MediaPlayer-GLES-FlowAbs')
    compile project(':MediaPlayer-GLES-QrMarker')
    compile('com.crashlytics.sdk.android:crashlytics:2.0.1@aar') {
        transitive = true;
    }
}

ext.isLibrary = false
apply from: "../gitversioning.gradle"
apply from: "signingconfig.gradle"

if(isChrashlyticsConfigured()) {
    apply plugin: 'io.fabric'
}

def isChrashlyticsConfigured() {
    return file("crashlytics.properties").exists()
}